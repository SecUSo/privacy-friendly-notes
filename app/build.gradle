apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

def pfaFile = rootProject.file('pfa.properties')

android {
    signingConfigs {
        if (pfaFile.exists()) {
            'pfa-key' {
                def pfa = new Properties()
                pfa.load(new FileInputStream(pfaFile))

                storePassword pfa['storePassword']
                keyAlias pfa['keyAlias']
                keyPassword pfa['keyPassword']
                storeFile file(pfa['storeFile'])
            }
        }
    }

    defaultConfig {
        applicationId "org.secuso.privacyfriendlynotes"
        minSdkVersion 21
        compileSdk 34
        targetSdkVersion 34
        versionCode 17
        versionName "1.4.4"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach {
            outputFileName = "pfa-notes-${variant.name}-v${variant.versionName}.apk"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            debuggable true
            if (pfaFile.exists()) {
                signingConfig signingConfigs.'pfa-key'
                println("Using PFA key")
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }

    namespace 'org.secuso.privacyfriendlynotes'
}

dependencies {
    implementation project(path: ':backup-api')
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.room:room-testing:2.6.0'
    testImplementation 'junit:junit:4.13.2'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.10.0'
    implementation 'com.getbase:floatingactionbutton:1.10.1'
    implementation 'com.simplify:ink:1.0.0'
    implementation 'petrov.kristiyan:colorpicker-library:1.1.10'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.1'

    def work_version = "2.8.1"
    implementation "androidx.work:work-runtime:$work_version"
    implementation "androidx.work:work-runtime-ktx:$work_version"
    androidTestImplementation "androidx.work:work-testing:$work_version"

    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    def room_version = "2.5.2"

    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"

    implementation "androidx.core:core-ktx:1.12.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.2"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
repositories {
    mavenCentral()
}
